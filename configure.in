dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.54)

dnl Name your plug-in here
m4_define([plugin_name], [gimp-inpaint-BCT])

dnl These three define the plug-in version number
m4_define([plugin_major_version], [0])
m4_define([plugin_minor_version], [2])
m4_define([plugin_micro_version], [0])

m4_define([plugin_version],
          [plugin_major_version.plugin_minor_version.plugin_micro_version])

AC_INIT([plugin_name], [plugin_version])

AC_DEFINE(PLUGIN_NAME, PACKAGE_NAME, [Plug-In name])
AC_DEFINE(PLUGIN_VERSION, PACKAGE_VERSION, [Plug-In version])

AC_DEFINE(PLUGIN_MAJOR_VERSION, plugin_major_version, [Plug-In major version])
AC_DEFINE(PLUGIN_MINOR_VERSION, plugin_minor_version, [Plug-In minor version])
AC_DEFINE(PLUGIN_MICRO_VERSION, plugin_micro_version, [Plug-In micro version])

dnl --------------------------------------------------------------------
dnl Directories.
dnl --------------------------------------------------------------------
AC_CONFIG_SRCDIR([src/main.c])
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(no-define)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_PROG_CXX


ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AM_MAINTAINER_MODE


dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl --------------------------------------------------------------------
dnl Compiling for Win32.
dnl --------------------------------------------------------------------
case $host in
  *mingw* )
    compile_for_win32=yes
    ;;
  * )
    compile_for_win32=no
    ;;
esac
AM_CONDITIONAL(COMPILE_FOR_WIN32, test x$compile_for_win32 = xyes)


dnl --------------------------------------------------------------------
dnl Extra options when compiling for Win32.
dnl --------------------------------------------------------------------
if test x$compile_for_win32 = xyes; then
  echo "we are compiling for win32, set CFLAGS and LIBS accordingly"
  CFLAGS="$CFLAGS -mwindows -mms-bitfields -static-libstdc++ -static-libgcc"
  CPPFLAGS="$CPPFLAGS -mwindows -mms-bitfields -static-libstdc++ -static-libgcc"
  # As of 2009-09-16, -lgthread-2.0 does not appear in the package
  # configuration of the Win32 Gimp developer package (download from
  # 'http://www.gimp.org/~tml/gimp/win32/gimp-dev-2.4.zip'). But the
  # library itself is there. See README for compiling for Win32.
  LIBS="$LIBS -lgthread-2.0"
fi

dnl --------------------------------------------------------------------
dnl Point to programs/libraries installed in a non-default place. This
dnl is necessary for cross-compiling for Win32, as the Win32 gimp
dnl stuff has no standard location.
dnl --------------------------------------------------------------------
AC_ARG_WITH([prefix],
  [AS_HELP_STRING([--with-prefix=PREFIX],
                  [use also PREFIX as an input prefix for the build])],
  [with_prefix=$withval],
  [with_prefix=NONE]
)

if test x$with_prefix != xNONE; then
    echo "Extending pkg-config path, cflags, and ldflags by dirs located in"
    echo "$with_prefix."
    export PKG_CONFIG_PATH="$with_prefix/lib/pkgconfig$PATH_SEPARATOR$PKG_CONFIG_PATH"
    CFLAGS="$CFLAGS -I$with_prefix/include"
    LDFLAGS="$LDFLAGS -L$with_prefix/lib"
fi


GIMP_REQUIRED_VERSION=2.2.0

PKG_CHECK_MODULES(GIMP,
  gimp-2.0 >= $GIMP_REQUIRED_VERSION gimpui-2.0 >= $GIMP_REQUIRED_VERSION)

AC_SUBST(GIMP_CFLAGS)
AC_SUBST(GIMP_LIBS)

dnl --------------------------------------------------------------------
dnl i18n stuff
dnl --------------------------------------------------------------------


GETTEXT_PACKAGE=gimp-inpaint-BCT
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",
				    [The gettext translation domain.])

dnl No translating done yet!!
dnl ALL_LINGUAS="az de fr sk sv zh_TW"
ALL_LINGUAS=""

dnl --------------------------------------------------------------------
dnl Definition of Gimp plug-in directories for user/admin installation.
dnl See 'm4/gimp-user-install.m4' for details.
dnl --------------------------------------------------------------------
GIMP_PLUGIN_DIRECTORIES

dnl --------------------------------------------------------------------
dnl AC_PROG_INTLTOOL
dnl --------------------------------------------------------------------
AM_GLIB_GNU_GETTEXT
AC_CHECK_FUNCS(bind_textdomain_codeset)

LOCALEDIR='${GIMP_PLUGIN_LOCALEDIR}'
DATADIR='${GIMP_PLUGIN_DATADIR}/plugin_name'
BINDIRREP='${GIMP_PLUGIN_BINDIR}'

AC_SUBST(LOCALEDIR)
AC_SUBST(DATADIR)
AC_SUBST(BINDIRREP)

AC_MSG_CHECKING([if GTK+ is version 2.7.0 or newer])
if $PKG_CONFIG --atleast-version=2.7.0 gtk+-2.0; then
  have_gtk_2_7=yes
else
  have_gtk_2_7=no
fi
AC_MSG_RESULT($have_gtk_2_7)

if test "x$have_gtk_2_7" != "xyes"; then
  CPPFLAGS="$CPPFLAGS -DG_DISABLE_DEPRECATED -DGDK_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED"
fi


AC_MSG_CHECKING([if GIMP is version 2.3.0 or newer])
if $PKG_CONFIG --atleast-version=2.3.0 gimp-2.0; then
  have_gimp_2_3=yes
else
  have_gimp_2_3=no
fi
AC_MSG_RESULT($have_gimp_2_3)

if test "x$have_gimp_2_3" != "xyes"; then
  CPPFLAGS="$CPPFLAGS -DGIMP_DISABLE_DEPRECATED"
fi



AC_CONFIG_FILES([
Makefile
src/Makefile
po/Makefile.in
help/Makefile
help/en/Makefile
help/images/Makefile
])

AC_OUTPUT

dnl --------------------------------------------------------------------
dnl Summary.
dnl --------------------------------------------------------------------

EVAL_localedir=`eval echo ${LOCALEDIR}`
EVAL_datadir=`eval echo ${DATADIR}`
EVAL_bindir=`eval echo ${BINDIRREP}`

AC_MSG_NOTICE([])
AC_MSG_NOTICE([====================== summary =====================])
AC_MSG_NOTICE([prefix:     ${prefix}])
AC_MSG_NOTICE([locale-dir: ${EVAL_localedir}])
AC_MSG_NOTICE([data-dir:   ${EVAL_datadir}])
AC_MSG_NOTICE([bin-dir:    ${EVAL_bindir}])

